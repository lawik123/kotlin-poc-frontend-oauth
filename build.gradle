plugins {
    id "kotlin2js" version "1.3.21"
    id 'kotlinx-serialization' version '1.3.21'
}

group 'kotlin-poc-frontend-oauth'
version '1.0-SNAPSHOT'

ext {
    web_dir = "web"
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.10.0"
}

clean.doFirst() {
    delete("${web_dir}")
}

task copyResources(type: Copy) {
    from "${buildDir}/resources/main/web"
    into "${web_dir}"
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${web_dir}"

    dependsOn classes
}

assemble.dependsOn assembleWeb
assemble.dependsOn copyResources
